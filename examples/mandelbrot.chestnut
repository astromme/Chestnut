parallel Integer mandelbrot(Real xleft, Real xright, Real ytop, Real ybottom) {
  // Scale coordinates to be in the given window
  Real x0 = (parallel.x/parallel.width) * (xright - xleft) + xleft;
  Real y0 = (parallel.y/parallel.height) * (ytop - ybottom) + ybottom;

  Real x = 0;
  Real y = 0;
  Integer iteration = 0;
  Integer max_iteration = 1000;

  while (((x*x + y*y) <= (2*2)) && (iteration < max_iteration)) {
    Real xtemp = (x*x - y*y) + x0;

    y = 2*x*y + y0;
    x = xtemp;
    iteration = iteration + 1;
  }

  if (iteration == max_iteration)
    return 0;         // Failed to diverge -- in the set
  else
    return iteration; // diverged in some number of iterations
}

Real left = -2.5;
Real right = 1;
Real top = 1;
Real bottom = -1;

Integer2d display_data[875, 500];

display_data = display_data.each(:mandelbrot(left, right, top, bottom));
display(display_data);


