parallel Int mandelbrot(Point2d location, Size2d arraySize, Real xleft, Real xright, Real ytop, Real ybottom) {
  // Scale coordinates to be in the given window
  Real x0 = (location.x/arraySize.width) * (xright - xleft) + xleft;
  Real y0 = (location.y/arraySize.height) * (ytop - ybottom) + ybottom;

  Real x = 0;
  Real y = 0;
  Int iteration = 0;
  Int max_iteration = 1000;

  while (((x*x + y*y) <= (2*2)) && (iteration < max_iteration)) {
    Real xtemp = (x*x - y*y) + x0;

    y = 2*x*y + y0;
    x = xtemp;
    iteration = iteration + 1;
  }

  if (iteration == max_iteration)
    return 0;         // Failed to diverge -- in the set
  else
    return iteration; // diverged in some number of iterations
}

parallel Color orange_convert(Int value) {
  Color c;
  c.red = min(255, 15*value);
  c.green = c.red / 2;
  c.blue = 0;
  c.opacity = 255;
  return c;
}

Real left = -2.5;
Real right = 1;
Real top = 1;
Real bottom = -1;

IntArray2d mandelbrot_display[875, 500];

foreach pixel in mandelbrot_display
  pixel = mandelbrot(location(pixel), mandelbrot_display.size, left, right, top, bottom);
end

display(mandelbrot_display, orange_convert);


